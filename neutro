use v6;

sub mkdirp (Str $what) {
	my @parts = $what.split('/').grep: { $_ };
	my $path;
	for @parts -> $dir {
		$path ~= "/$dir";
		next if $path.IO ~~ :e;
		mkdir $path;
	}
}

sub notice (Str $what) {
	say "\e[1m==> ", $what, "\e[0m";
}

sub MAIN (Str $url) {
	my $res; # for various run() calls
	$url ~~ /(<-[\/]>+).git/;
	my $name = $/[0];
	my $workdir = "%*ENV<HOME>/.neutro/src/$name";
	if $workdir.IO ~~ :e {
		chdir $workdir;
		notice "Updating $name";
		$res = run 'git pull';
		die "Failed updating the repo!" if $res;
	} else {
		mkdirp $workdir;
		chdir $workdir;
		notice "Cloning $name";
		$res = run "git clone $url .";
		die "Failed cloning the repo!" if $res;
	}
	unless 'lib'.IO ~~ :d {
		die "No lib/, nothing to do"
	}
	if 't'.IO ~~ :d {
		notice 'Testing';
		$res = run 'PERL6LIB=lib:$PERL6LIB prove -e `which perl6` t/';
		die "Tests failed" if $res;
	}
	notice 'Installing';
	# making a list of module files to install
	my @targets = dir 'lib';
	chdir 'lib';
	my @modulefiles;
	while @targets {
		my $elem = @targets.shift;
		if $elem.IO ~~ :d {
			for dir($elem) -> $file {
				@targets.push: "$elem/$file";
			}
		}
		if $elem ~~ /\.pm6?$$/ {
			@modulefiles.push: $elem;
		}
	}
	for @modulefiles -> $file {
		my $path = "%*ENV<HOME>/.perl6/lib/" ~ $file;
		my $dir = $path.substr(0, $path.rindex('/'));
		mkdirp $dir;
		run "cp $file $dir";
		say "Installed -> $path";
	}
	notice "Succesfully installed $name";
}

sub USAGE {
	say "Usage: neutro <git repo url>"
}

# vim: ft=perl6
