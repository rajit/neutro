#!/usr/bin/env perl6
use v6;

my $CONFIGDIR = "%*ENV<HOME>/.neutro";
my $INSTALLDIR = "%*ENV<HOME>/.perl6";
my %modules;

# check if modules list is present, update it otherwise
sub checklist {
	unless "$CONFIGDIR/modules.list".IO ~~ :f {
		updatedb;
	}
	my $list = slurp "$CONFIGDIR/modules.list";
	my @lines = $list.split("\n");
	for @lines -> $line {
		my ($name, $protoname, $url) = $line.split: ' ';
		%modules{$name}<protoname> = $protoname;
		%modules{$name}<url> = $url;
	}
}

sub install (Str $name) {
	my $res;
	unless %modules.exists($name) {
		die "Unknown module $name";
	}
	my $workdir = "$CONFIGDIR/src/$name";
	if $workdir.IO ~~ :e {
		chdir $workdir;
		notice "Updating $name";
		$res = run 'git pull';
		die "Failed updating the repo!" if $res;
	} else {
		mkdirp $workdir;
		chdir $workdir;
		notice "Cloning $name";
		$res = run "git clone %modules{$name}<url> .";
		die "Failed cloning the repo!" if $res;
	}
	unless 'lib'.IO ~~ :d {
		die "No lib/, nothing to do"
	}
	if 't'.IO ~~ :d {
		notice 'Testing';
		$res = run 'PERL6LIB=lib:$PERL6LIB prove -e `which perl6` t/';
		die "Tests failed" if $res;
	}
	notice 'Installing';
	# making a list of module files to install
	my @targets = dir 'lib';
	chdir 'lib';
	my @modulefiles;
	while @targets {
		my $elem = @targets.shift;
		if $elem.IO ~~ :d {
			for dir($elem) -> $file {
				@targets.push: "$elem/$file";
			}
		}
		if $elem ~~ /\.pm6?$$/ {
			@modulefiles.push: $elem;
		}
	}
	for @modulefiles -> $file {
		my $path = "$INSTALLDIR/lib/" ~ $file;
		my $dir = $path.substr(0, $path.rindex('/'));
		mkdirp $dir;
		run "cp $file $dir";
		say "Installed -> $path";
	}
	notice "Succesfully installed $name";
}

sub listmodules {
	for %modules.keys.sort { .say }
}

sub mkdirp (Str $what) {
	my @parts = $what.split('/').grep: { $_ };
	my $path;
	for @parts -> $dir {
		$path ~= "/$dir";
		next if $path.IO ~~ :e;
		mkdir $path;
	}
}

sub notice (Str $what) {
	say "\e[1m==> ", $what, "\e[0m";
}

sub updatedb {
	notice "Updating modules database";
	chdir $CONFIGDIR;
	# downloading; TODO: Portable solution
	run 'wget http://github.com/tadzik/neutro/raw/master/modules.list';
}

sub MAIN ($command, $param?) {
	checklist;
	given $command {
		when 'i' {
			unless $param.defined {
				die "i requires a parameter -- a module name";
			}
			install $param;
		}
		when 'l' {
			listmodules;
		}
	}
}

sub USAGE {
	say "Usage: neutro <command> [parameter]";
	say "Available commands (with example usage):
	neutro i Acme::Meow -- install Acme::Meow
	neutro l            -- list available modules
";
}

# vim: ft=perl6
